#!/bin/csh -f

# script for scanning urls of current modis products

# Prof. P. Lewis, UCL
# Oct 20 2013
# email: p.lewis@ucl.ac.uk


# Zat is the name of a friendly robot in a John Wyndham
# story, The Lost Machine (Amazing Stories, April 1932).
# 

set server = 'ftp://n4ftl01u.ecs.nasa.gov/'
set tmdir = robot_files_snow

cd /tmp

mkdir -p $tmdir
cd $tmdir

foreach f (MOSA MOST)
  if ( ! -e $f ) wget -O $f $server/SAN/$f/
  set products = `grep 'href=' < $f | grep -v DPRecentInserts| awk -Fhref=\" '{print $2}'|awk -F/\" '{print $1}'`
  foreach p ($products)
    set pp = $p:t
    if ( ! -e $pp ) wget -O $pp $p/
    set dates = `grep 'href=' < $pp | grep -v DPRecentInserts|  awk -Fhref=\" '{print $2}'|awk -F\"\> '{print $1}'`
    foreach d ($dates)
      set dd = $d:t
      if ( ! -e $pp.$dd ) wget -O $pp.$dd $d/
      set files = (`grep hdf < $pp.$dd | grep -v DPRecentInserts| grep -v xml | awk -Fhref=\" '{print $2}'|awk -F\"\> '{print $1}'`)
      foreach fi ($files)
        echo $fi >> robot.txt
      end
    end
  end
end

cat robot.txt
cd /tmp
rm -rf $tmdir


