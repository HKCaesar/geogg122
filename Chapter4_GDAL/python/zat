#!/bin/csh -f

# script for scanning urls of current modis products

# Prof. P. Lewis, UCL
# Oct 20 2013
# email: p.lewis@ucl.ac.uk


# Zat is the name of a friendly robot in a John Wyndham
# story, The Lost Machine (Amazing Stories, April 1932).
# 

cd /tmp

mkdir -p robot_files
cd robot_files

foreach f (MOLA MOLT MOTA)
  if ( ! -e $f ) wget http://e4ftl01.cr.usgs.gov/MODIS_Composites/$f
  set products = `grep 'alt=' < $f | grep -v blank | awk -Fhref=\" '{print $2}'|awk -F/\" '{print $1}'`
  foreach p ($products)
    if ( ! -e $p ) wget http://e4ftl01.cr.usgs.gov/MODIS_Composites/$f/$p
    set dates = `grep 'alt=' < $p | grep -v blank | awk -Fhref=\" '{print $2}'|awk -F/\" '{print $1}'`
    foreach d ($dates)
      if ( ! -e $p.$d ) wget -O $p.$d http://e4ftl01.cr.usgs.gov/MODIS_Composites/$f/$p/$d
      set files = (`grep hdf < $p.$d | grep -v xml | awk -Fhref=\" '{print $2}'|awk -F\"\> '{print $1}'`)
      foreach fi ($files)
        echo "http://e4ftl01.cr.usgs.gov/MODIS_Composites/$f/$p/$d/$fi" >> robot.txt
      end
    end
  end
end

cat robot.txt
cd /tmp
rm -rf robot_files


